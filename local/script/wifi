#!/bin/zsh
[ "${ZSH_VERSION:-}" = "" ] && echo >&2 "Only works with zsh" && exit 1
setopt err_exit no_unset no_clobber pipefail

dev=${DEV:-}
[[ -z $dev ]] && dev=$(iw dev | grep Interface | head -n1 | cut -d ' ' -f2)

usage() {
	[[ -n ${argv[1]:-} ]] && print >&2 "wifi: error: ${argv[1]}\n"
	<<-EOF
	wifi [command] [cmd-args]

	Using device $dev; set DEV to change it.

	Commands:
	   status [refresh]   Current status (default is no cmd is given).
	                      Add a number to keep refreshing every n seconds.
	   scan               Scan for networks.
	   monitor            Keep scanning for networks.
	   connect [id] [pwd] Connect to SSID or BSS; use wpa_supplicant if [pwd] is set.
	   addr [ip] [route]  Manually set IP address with default route.
	EOF
	[[ -n ${argv[1]:-} ]] && exit 1
	return 0
}

up() {
    local f=$(ip link | grep ${dev}:)
    [[ -z $f ]] && usage "no such device: $dev"

    err=$(grep -q 'state DOWN' <<<$f && ip link set $dev up 2>&1 ||:)
    [[ -n $err ]] && print >&2 "wifi: warning: bringing device up: $err"
    return 0
}

status() {
    # Refresh: keep calling this status function (w/o arguments).
    if [[ -n ${1:-} ]]; then
        while :; do
            s=$(status)  # Less flicker this way, as running ip/iw takes a few ms.
            clear
            print $s
            sleep $1
        done
        return
    fi

	local link=$(iw dev $dev link)

	# TODO: dev:    wlp0s2 (23:23...)
	print -f '%-12s %s\n' 'dev'   "$dev"
	print -f '%-12s %s\n' 'ip'    "$(ip addr show dev $dev | grep '    inet ' | cut -d ' ' -f6)"
	print -f '%-12s %s\n' 'route' "$(ip route list dev $dev | sed '2,0s/^/             /')"
	print -f '%-12s %s\n' 'bss'   "$(head -n1 <<<$link | cut -d ' ' -f3)"

	iw dev $dev info  | tail -n+2 | awk -F ' ' \
		'/(addr|ssid|channel|txpower) / { printf("%-12s", $1); $1=""; print $0 }'

	print $link | awk -F ':' \
		'/(signal|[rt]x bitrate):/ { printf("%-12s", gensub(/^\s/, "", "g", $1)); $1=""; print gensub(/^\s/, "", "g", $0) }'

	print -f '%-12s %sM\n' 'total RX' $(( ${$(grep -o 'RX: [0-9]*' <<<$link || print 0)#*:} / 1048576))
	print -f '%-12s %sM\n' 'total TX' $(( ${$(grep -o 'TX: [0-9]*' <<<$link || print 0)#*:} / 1048576))
}

scan() {
	local connected=$(iw dev $dev link | head -n1 | cut -d ' ' -f3)
	iw dev $dev scan | awk -F: "$(<<-'EOF'
		/^BSS / {
			if (ssid != "")
				printf("  %-10s  %s  %-30s (%s)\n", signal, bss, ssid, ciphers)
			bss = sprintf("%s:%s:%s:%s:%s:%s", substr($1, 5, 6), $2, $3, $4, $5, substr($6, 0, 2))
		}
		/^\tsignal:/        { signal  = gensub(/^ /, "", "g", $2) }
		/^\tSSID:/          { ssid    = gensub(/^ /, "", "g", $2) }
		/Pairwise ciphers:/ { ciphers = gensub(/^ /, "", "g", $2) }
	EOF
	)" | sort -nr | sed -E "s/  (.*?$connected)/â†’ \1/"
}

connect() {
	local ssid=${argv[1]:-}
    local pass=${argv[2]:-}
	[[ -z $ssid ]] && usage 'need a SSID'

	iw dev $dev disconnect
	if grep -Eq '[0-9a-fA-F]{2}(:[0-9a-fA-F]{2}){5}' <<<$ssid; then
		iw dev $dev connect -w bssid $ssid
	else
		iw dev $dev connect -w $ssid
	fi

    [[ -n $pass ]] && wpa_cli passphrase $ssid $pass
	# wpa_cli save_config
}

addr() {
	local ip=${argv[1]:-}
	local route=${argv[2]:-}
	[[ -z $ip ]] && usage 'need an IP address\n'

	ip addr add $ip dev $dev
	[[ -n $route ]] && ip route add default via 10.0.1.1 dev $dev
}

monitor() {
	while :; do
		s=$(scan)
		clear
		print $s
		sleep 2
	done
}

cmd=status
if [[ -n ${argv[1]:-} ]]; then
	cmd=$argv[1]
	shift
fi

case $cmd in
	(help|-h|-help) usage                ;;
	(scan)          up; scan    $argv[@] ;;
	(connect)       up; connect $argv[@] ;;
	(addr)          up; addr    $argv[@] ;;
	(monitor)       up; monitor $argv[@] ;;
	(status)        up; status  $argv[@] ;;
	(*)
		print >&2 "unknown command: $cmd\n"
		usage
		exit 1
esac
