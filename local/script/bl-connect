#!/bin/zsh
#
# Connect to paired bluetooth devices.
#
# Just bl-connect will ask for a device to connect to; use 'bl-connect $dev' (as
# number or name) to connect to that device without asking.
echo " $@" | grep -q -- ' -h' && { sed '1,2d; /^[^#]/q; s/^# \?//;' "$0" | sed '$d'; exit 0; }  # Show docs
[ "${ZSH_VERSION:-}" = "" ] && echo >&2 "Only works with zsh" && exit 1
setopt err_exit no_unset no_clobber pipefail

connect() {
	local mac=$(cut -d ' ' -f2 <<<$argv[1])

    # Otherwise audio devices can't connect; usually started on the first audio
    # application, but this may not have happened yet.
	pulseaudio --start

	bluetoothctl <<<'power on'
	bluetoothctl <<<"connect $mac"
}

IFS=$'\n' devs=($(<<<devices bluetoothctl | grep '^Device'))
if [[ -n ${1:-} ]]; then
	for dev in $devs; do
		if print "$dev" | grep -iq "$1$"; then
			connect "$dev"
			break
		fi
	done
	exit 0
fi

i=0
for dev in $(cut -d ' ' -f3- <<<$devs); print "$(( i=i+1 )) $dev"

print -n "Which? "
read n
connect $(sed "${n}p;d" <<<$devs)
