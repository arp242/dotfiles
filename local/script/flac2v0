#!/bin/zsh
#
# Convert all FLAC files in a directory to MP3 V0. Requires the "flac" and
# "lame" tools.
#
# Usage: flag2v0 [flac-dir] [flac-dir]
#
# MP3 files will be placed in a mp3 subdirectory.
echo " $@" | grep -q -- ' -h' && { sed '1,2d; /^[^#]/q; s/^# \?//;' "$0" | sed '$d'; exit 0; }  # Show docs
setopt err_exit no_unset no_clobber pipefail

_tag() {
	metaflac "$1" --show-tag="$2" | cut -d= -f2-
}

if [[ -z $1 ]]; then 
	print >&2 'flac2v0: need directory; use -h for help.'
	exit 2
fi

for flacdir in "$@"; do
	outdir="$flacdir/mp3"
	mkdir -p "$outdir"

	for f in $flacdir/*.flac; do
		[[ ! -e $f ]] && continue

		basename "$f"
		flac -dcs "$f" |
			lame --quiet --vbr-new -V0 --replaygain-accurate --id3v2-only \
				--tt "$(_tag "$f" TITLE)" \
				--tn "$(_tag "$f" TRACKNUMBER)" \
				--tg "$(_tag "$f" GENRE)" \
				--ty "$(_tag "$f" DATE)" \
				--ta "$(_tag "$f" ARTIST)" \
				--tl "$(_tag "$f" ALBUM)" \
				- "$outdir/$(basename "${f%.flac}").mp3"
	done

	# Copy additional files
	for f in "$flacdir"/*~*.flac; do
		cp "$f" "$outdir"
	done
done
