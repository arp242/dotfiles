#!/bin/zsh
#
# Set the active screen.
#
#   set-screen laptop
#   set-screen hdmi
#   set-screen switch
#   set-screen clone (or both)  # as a mirror/copy
#   set-screen laptop-hdmi      # Laptop left, HDMI right
#   set-screen hdmi-laptop      # HDMI left, laptop right
echo " $@" | grep -q -- ' -h' && { sed '1,2d; /^[^#]/q; s/^# \?//;' "$0" | sed '$d'; exit 0; }  # Show docs
[ "${ZSH_VERSION:-}" = "" ] && echo >&2 "Only works with zsh" && exit 1
setopt err_exit no_unset no_clobber pipefail

laptop="eDP-1"
laptop_mode=(--auto)
laptop_scale=0.8

hdmi="HDMI-2"
hdmi_mode=(--auto)
hdmi_scale=1

laptop_args() {
	print -- "--output $laptop $laptop_mode --scale ${laptop_scale}x${laptop_scale}"
}
hdmi_args() {
	print -- "--output $hdmi $hdmi_mode --scale ${hdmi_scale}x${hdmi_scale}"
}
active() {
	xrandr --query | grep ' connected primary ' | cut -d' ' -f1
}

case "${1:-}" in
	(hdmi)
		xrandr \
			$(hdmi_args) --pos 0x0 --primary \
			--output "$laptop" --off

		# Make sure new keyboard has mappings applied.
		xmodmap ~/.config/x11/modmap

		#pactl set-card-profile 0 output:hdmi-stereo-extra1
		#pactl set-card-profile 0 output:analog-stereo+input:analog-stereo
		;;
	(laptop)
		xrandr \
			--output "$hdmi" --off \
			$(laptop_args) --pos 0x0 --primary
		#pactl set-card-profile 0 output:analog-stereo+input:analog-stereo
		;;
	(switch)
		case $(active) in
			($laptop) set-screen hdmi   ;;
			($hdmi)   set-screen laptop ;;
			(*)
				print >&2 "set-screen: can't get active screen"
				exit 1
			;;
		esac
		;;
	(clone|both)
		xrandr \
			$(hdmi_args)   --pos 0x0 --primary \
			$(laptop_args) --pos 0x0
		#pactl set-card-profile 0 output:hdmi-stereo-extra1
		#pactl set-card-profile 0 output:analog-stereo+input:analog-stereo
		;;
	(laptop-hdmi)
		xrandr \
			$(laptop_args) --pos 0x0    --rotate normal \
			$(hdmi_args)   --pos 1920x0 --rotate normal --primary
			;;
	(hdmi-laptop)
		xrandr \
			$(hdmi_args)   --pos 0x0    --rotate normal --primary \
			$(laptop_args) --pos 1920x0 --rotate normal
		;;
	(*)
		# TODO: print status instead?
		print >&2 "${0##*/}: unknown screen; use -h for help"
		exit 1
esac
